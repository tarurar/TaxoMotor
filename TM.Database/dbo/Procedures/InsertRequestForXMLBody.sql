CREATE PROCEDURE [dbo].[InsertRequestForXMLBody]
	@SOURCEXQUERY NVARCHAR(MAX), -- root, example: N'/CoordinateMessage'
	@XMLBODY XML,
	@RECORDID INT OUTPUT
AS
	DECLARE @MESSAGEID VARCHAR(36);
	DECLARE @DECLARANT_ACCOUNT_ID INT;
	DECLARE @DECLARANT_ACCOUNT_XQUERY NVARCHAR(MAX);
	DECLARE @DECLARANT_CONTACT_ID INT;
	DECLARE @DECLARANT_CONTACT_XQUERY NVARCHAR(MAX);
	DECLARE @TRUSTEE_ID INT;
	DECLARE @TRUSTEE_XQUERY NVARCHAR(MAX);
	DECLARE @SERVICE_PROPS_ID INT;
	DECLARE @SERVICE_PROPS_XQUERY NVARCHAR(MAX);
	DECLARE @SERVICE_ID INT;
	DECLARE @SERVICE_XQUERY NVARCHAR(MAX);
	DECLARE @SERVICE_HEADER_ID INT;
	DECLARE @SERVICE_HEADER_XQUERY NVARCHAR(MAX);

	SET @MESSAGEID = [dbo].[fn_GetCoordinateV5RequestMessageId](@XMLBODY);
	SET @DECLARANT_ACCOUNT_ID = NULL;
	SET @DECLARANT_CONTACT_ID = NULL;
	SET @TRUSTEE_ID = NULL;
	SET @SERVICE_PROPS_ID = NULL;
	SET @SERVICE_ID = NULL;
	SET @SERVICE_HEADER_ID = NULL;
	SET @DECLARANT_ACCOUNT_XQUERY = CONCAT(@SOURCEXQUERY, N'/Message/Declarant[@xsi:type="RequestAccount"]');
	SET @DECLARANT_CONTACT_XQUERY = CONCAT(@SOURCEXQUERY, N'/Message/Declarant[@xsi:type="RequestContact"]');
	SET @TRUSTEE_XQUERY = CONCAT(@SOURCEXQUERY, N'/Message/Trustee');
	SET @SERVICE_PROPS_XQUERY = CONCAT(@SOURCEXQUERY, N'/Message/CustomAttributes/ServiceProperties');
	SET @SERVICE_XQUERY = CONCAT(@SOURCEXQUERY, N'/Message/Service');
	SET @SERVICE_HEADER_XQUERY = CONCAT(@SOURCEXQUERY, N'/ServiceHeader');
	
	-- ЗАЯВИТЕЛЬ - ЮРИДИЧЕСКОЕ ЛИЦО
	EXEC [dbo].[InsertRequestAccountForXMLBody] @DECLARANT_ACCOUNT_XQUERY, @XMLBODY, @DECLARANT_ACCOUNT_ID OUTPUT;
	-- ЗАЯВИТЕЛЬ - ФИЗИЧЕСКОЕ ЛИЦО
	EXEC [dbo].[InsertRequestContactForXMLBody] @DECLARANT_CONTACT_XQUERY, @XMLBODY, @DECLARANT_CONTACT_ID OUTPUT;
	-- ДОВЕРЕННОЕ ЛИЦО
	EXEC [dbo].[InsertRequestContactForXMLBody] @TRUSTEE_XQUERY, @XMLBODY, @TRUSTEE_ID OUTPUT;
	-- СПИСОК АВТОМАШИН ТАКСИ И SERVICE PROPERTIES (CustomAttributes)
	EXEC [dbo].[InsertServicePropertiesForXMLBody] @SERVICE_PROPS_XQUERY, @XMLBODY, @SERVICE_PROPS_ID OUTPUT;
	-- SERVICE
	EXEC [dbo].[InsertServiceForXMLBody] @SERVICE_XQUERY, @XMLBODY, @SERVICE_ID OUTPUT;
	-- SERVICE HEADER
	EXEC [dbo].[InsertServiceHeaderForXMLBody] @SERVICE_HEADER_XQUERY, @XMLBODY, @SERVICE_HEADER_ID OUTPUT;

	DECLARE @IDS TABLE([ID] INT);
	-- ЗАЯВКА
	INSERT INTO [dbo].[Request] (
		[MessageId],
		[DeclarantRequestAccount],
		[DeclarantRequestContact],
		[TrusteeRequestContact],
		[ServiceProperties],
		[Service],
		[ServiceHeader]
		)
	OUTPUT INSERTED.ID 
	INTO @IDS
	VALUES (
		@MESSAGEID,
		@DECLARANT_ACCOUNT_ID,
		@DECLARANT_CONTACT_ID,
		@TRUSTEE_ID,
		@SERVICE_PROPS_ID,
		@SERVICE_ID,
		@SERVICE_HEADER_ID
		);

	SELECT @RECORDID = [ID] FROM @IDS;

RETURN 0
