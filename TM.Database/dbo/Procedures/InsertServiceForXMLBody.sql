CREATE PROCEDURE [dbo].[InsertServiceForXMLBody]
	@SOURCEXQUERY NVARCHAR(MAX),
	@XMLBODY XML,
	@RECORDID INT OUTPUT
AS
	DECLARE @VALUES tt_KeyValueMap;
	DECLARE @SD_VALUES tt_KeyValueMap;
	DECLARE @FILE_VALUES tt_KeyValueMap;
	DECLARE @SERVICE_DOCUMENT_XQUERY NVARCHAR(MAX);
	DECLARE @FILE_XQUERY NVARCHAR(MAX);
	DECLARE @LAST_FILE_ID INT;

	SET @RECORDID = NULL;
	SET @SERVICE_DOCUMENT_XQUERY = CONCAT(@SOURCEXQUERY, N'/Documents/ServiceDocument');

	-- ЗАПИСЬ АТТРИБУТОВ ОКАЗЫВАЕМОЙ ГОС УСЛУГИ
	EXEC [dbo].[InsertTMRecordForXMLBody] N'DBO', N'SERVICE', @VALUES, @SOURCEXQUERY, @XMLBODY, @RECORDID OUTPUT;

	-- СПИСОК SERVICE DOCUMENTS
	DECLARE @SD_CURRENT_XML_ID NVARCHAR(255);
	DECLARE @SD_CURRENT_STORED_ID INT;
	DECLARE @SD_CURRENT_PATH NVARCHAR(255);

	DECLARE SERVICE_DOCUMENTS CURSOR
	FOR
	SELECT T.REF.value(N'Id[1]', 'NVARCHAR(255)') 
	FROM @XMLBODY.nodes(N'//Documents/ServiceDocument') T(REF);

	OPEN SERVICE_DOCUMENTS;

	FETCH NEXT
	FROM SERVICE_DOCUMENTS
	INTO @SD_CURRENT_XML_ID;

	WHILE @@FETCH_STATUS = 0
	BEGIN
		SET @SD_CURRENT_PATH = CONCAT(@SERVICE_DOCUMENT_XQUERY, N'[Id="' + @SD_CURRENT_XML_ID + '"]');
		DELETE FROM @SD_VALUES;
		INSERT INTO @SD_VALUES([KEY], [VALUE_INT]) VALUES(N'Service', @RECORDID);
		-- SERVICE DOCUMENT
		EXEC [dbo].[InsertTMRecordForXMLBody] N'DBO', N'SERVICEDOCUMENT', @SD_VALUES, @SD_CURRENT_PATH, @XMLBODY, @SD_CURRENT_STORED_ID OUTPUT;
		DELETE FROM @FILE_VALUES;
		INSERT INTO @FILE_VALUES([KEY], [VALUE_INT]) VALUES(N'ServiceDocument', @SD_CURRENT_STORED_ID);
		SET @FILE_XQUERY = CONCAT(@SD_CURRENT_PATH, N'/DocFiles/File');
		-- FILES LINKED TO SERVICE DOCUMENT
		EXEC [dbo].[InsertTMRecordForXMLBody] N'DBO', N'FILE', @FILE_VALUES, @FILE_XQUERY, @XMLBODY, @LAST_FILE_ID OUTPUT;
		
		FETCH NEXT
		FROM SERVICE_DOCUMENTS
		INTO @SD_CURRENT_XML_ID;
	END

	CLOSE SERVICE_DOCUMENTS;

	DEALLOCATE SERVICE_DOCUMENTS;

RETURN 0
